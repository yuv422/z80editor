grammar org.efry.z80editor.Z80 with org.eclipse.xtext.common.Terminals

generate z80 "http://www.efry.org/z80editor/Z80"

Model:
    commands+=Line*;


//terminal REGISTER_A: 'a'|'A';
//terminal REGISTER_B: 'b'|'B';
//REGISTER_C: 'c'|'C'; //cannot be a terminal because it clashes with the 'C'arry flag.
//terminal REGISTER_D: 'd'|'D';
//terminal REGISTER_E: 'e'|'E';
//terminal REGISTER_H: 'h'|'H';
//terminal REGISTER_I: 'i';
//terminal REGISTER_L: 'l'|'L';
//terminal REGISTER_R: 'r';
//
//terminal REGISTER_AF: 'af'|'Af'|'aF'|'AF';
//terminal REGISTER_AF_SHADOW: 'af\''|'Af\''|'aF\''|'AF\'';
//terminal REGISTER_BC: 'bc'|'Bc'|'bC'|'BC';
//REGISTER_DE: 'de'|'De'|'dE'|'DE';
//terminal REGISTER_HL: 'hl'|'Hl'|'hL'|'HL';
//terminal REGISTER_IX: 'ix'|'Ix'|'iX'|'IX';
//terminal REGISTER_IY: 'iy'|'Iy'|'iY'|'IY';
//terminal REGISTER_SP: 'sp'|'Sp'|'sP'|'SP';
terminal REGISTER_A: 'a';
terminal REGISTER_B: 'b';
REGISTER_C: 'c'; //cannot be a terminal because it clashes with the 'C'arry flag.
terminal REGISTER_D: 'd';
terminal REGISTER_E: 'e';
terminal REGISTER_H: 'h';
terminal REGISTER_I: 'i';
terminal REGISTER_L: 'l';
terminal REGISTER_R: 'r';

terminal REGISTER_AF: 'af';
terminal REGISTER_AF_SHADOW: 'af\'';
terminal REGISTER_BC: 'bc';
REGISTER_DE: 'de';
terminal REGISTER_HL: 'hl';
terminal REGISTER_IX: 'ix';
terminal REGISTER_IY: 'iy';
terminal REGISTER_SP: 'sp';

terminal NEWLINE:
// New line on DOS or Unix
    '\r'? '\n';
  
//Line:
//	Define NEWLINE |
//	MemoryMap NEWLINE |
//	RomBankMap NEWLINE |
//	Sdsctag NEWLINE |
//	Bank NEWLINE |
//	Org NEWLINE |
//	Section NEWLINE |
//	RamSection NEWLINE |
//	Rept NEWLINE |
//	Db NEWLINE |
//	Dw NEWLINE |
//	Include NEWLINE |
//	Incbin NEWLINE |
//	//UnHandledWlaDxCommand NEWLINE |
//    (Label | Command?) NEWLINE |
//    l=Label cmd=Command NEWLINE;// |
//   // NEWLINE;

Line:
	(Define | MemoryMap | RomBankMap | Sdsctag | Bank | Org | Section | RamSection | Rept | Db | Dw | Include | Incbin | Label | Command) NEWLINE | (Label c=Command NEWLINE) | {EmptyLine} NEWLINE;


Define: '.define' name=VarName expr=Expression;

MemoryMap: '.memorymap' NEWLINE cmds+=MemoryMapCommands+ '.endme';
MemoryMapCommands: ('defaultslot' INT NEWLINE) |
	('slotsize' (HEX|INT) NEWLINE) |
	('slot' (HEX|INT) (HEX|INT)? NEWLINE)
;

RomBankMap: '.rombankmap' NEWLINE cmds+=RomBankCommands+ '.endro';
RomBankCommands: ('bankstotal' (INT|HEX) NEWLINE) |
	('banksize' (INT|HEX) NEWLINE) |
	('banks' (INT|HEX) NEWLINE)
;

Rept: '.rept' INT NEWLINE block+=Line+ '.endr';
Bank: {BankCmd} '.bank' INT ('slot' slot=INT)?;
Org: '.org' value=(NUMBER);
Section: '.section' sectionName=STRING? ('size' size=NUMBER)? ('align' align=NUMBER)? ('force'|'semifree'|'free'|'superfree'|'overwrite')? NEWLINE lines+=Line+ '.ends';

RamSection: '.ramsection' sectionName=STRING ('bank' bank=NUMBER)? ('slot' slot=NUMBER)? NEWLINE definitions+=VarDefinition* '.ends';

VarDefinition: {VarDef} (varName=VarName type=(VarTypeByte|VarTypeWord|VarTypeByteString|VarTypeWordString))? NEWLINE;

VarTypeByte: {VarType} 'db';
VarTypeWord: {VarType} 'dw';
VarTypeByteString: {VarType} 'ds'|'dsb' size=Expression (',' initalValue=Expression)?;
VarTypeWordString: {VarType} 'dsw' size=Expression (',' initalValue=Expression)?;

Db: {DbCmd} '.db' (INT|HEX|BINARY) (','? (INT|HEX|BINARY))*;
Dw: {DwCmd} '.dw' (INT|HEX|BINARY) (','? (INT|HEX|BINARY))*;
Include: '.include' file=STRING;
Incbin: '.incbin' file=STRING;

Sdsctag: {SdscTagCmd} '.sdsctag' INT('.'INT)? ',' (STRING|ID) ',' (STRING|ID) ',' (STRING|ID); //FIXME need to make a better version number type

//terminal UnHandledWlaDxCommand: '.' ID !('\n'|'\r')*; //FIXME

PLUS: '+';
terminal PLUSLABEL: '+:';
terminal PLUSPLUS: '+''+'+;

terminal MINUSLABEL: '-:';
terminal MINUS: '-';
terminal MINUSMINUS: '-''-'+;

BANKLABEL hidden(): ':'referencedObj=[VarName];

VarName: name=ID;

Label hidden() : {LabelType} (PLUSLABEL|MINUSLABEL|((varName=VarName|PLUSPLUS|MINUSMINUS)':'));

Expression returns Expr:
    BitwiseOp;

BitwiseOp returns Expr:
    Addition
    ({BinaryExpr.left=current} op1=('|' | '&') right=Addition)*;

Addition returns Expr:
    Multiplication
    ({BinaryExpr.left=current} op1=(PLUS | MINUS) right=Multiplication)*;

Multiplication returns Expr:
    PrimaryExpression
    ({BinaryExpr.left=current} op1=('*' | '/') right=PrimaryExpression)*;

PrimaryExpression returns Expr:
    '(' Expression ')' |
    {NumLiteral} sign=MINUS? value=NumericLiteral;
      
Command:
     (Adc|Add|And|Bit|Call|Ccf|Cp|Daa|Dec|Di|Djnz|Ei|Ex|Exx|Halt|Im|In|Inc|Ind|Indr|Ini|Inir|Jp|Jr|Ld|Ldd|Lddr|Ldi|Ldir|Neg|Nop|Or|Otdr|Otir|Out|Outi|Outd|Pop|Push|Res|Ret|Reti|Retn|Rl|Rla|Rlc|Rlca|Rld|RrCmd|Rra|Rrca|Rrd|Rst|Sbc|Set|Sla|Sra|Srl|Sub|Xor)
;

DoubleRegisters:
    (REGISTER_AF|REGISTER_BC|REGISTER_DE|REGISTER_HL|xx)
;

Adc: {Operation} opcode='adc' ((REGISTER_A ',' rightOp=s)|(REGISTER_HL ',' rightOp=ss));
Add: {Operation} opcode='add' ((REGISTER_A ',' rightOp=s)|(REGISTER_HL ',' rightOp=ss)|(REGISTER_IX ',' rightOp=pp)|(REGISTER_IY ',' rightOp=rr));
And: {Operation} opcode='and' operand=s;
Bit: {Operation} opcode='bit' leftOp=B ',' rightOp=m;
Call: {Operation} opcode='call' (CC ',')? literal=NumericLiteral;
Ccf: {Operation} opcode='ccf';
Cp: {Operation} opcode='cp' operand=s;
Daa: {Operation} opcode='daa';
Dec: {Operation} opcode='dec' operand=(s|ss)|indexReg=xx;
Di: {Operation} opcode='di';
Djnz: {Operation} opcode='djnz' operand=e;
Ei: {Operation} opcode='ei';
Ex: {Operation} opcode='ex' ((SPIndirect ',' REGISTER_HL)|(SPIndirect ',' xx)|(REGISTER_AF ',' REGISTER_AF_SHADOW)|(REGISTER_DE ',' REGISTER_HL));
Exx: {Operation} opcode='exx';
Halt: {Operation} opcode='halt';
Im: {Operation} opcode='im' literal=NumericLiteral;
In: {Operation} opcode='in' ((REGISTER_A ',' rightOp=NumericLiteralIndirect)|(leftOp=r ',' CIndirect));
Inc: {Operation} opcode='inc' operand=incOps;
Ind: {Operation} opcode='ind';
Indr: {Operation} opcode='indr';
Ini: {Operation} opcode='ini';
Inir: {Operation} opcode='inir';
Jp: {Operation} opcode='jp' (reg=(HLIndirect|IndexIndirect)|((jumpCondition=CC ',')? (lit=NumericLiteral|PLUS|PLUSPLUS|MINUS|MINUSMINUS)));
Jr: {Operation} opcode='jr' (jumpCondition=jrCc ',')? operand=e;
Ld: {Operation} opcode='ld' leftOp=dst ',' rightOp=src; //FIXME src shouldn't contain 'i','r'. It should look like this 'ld' ((dst ',' src)|(REGISTER_A ',' ('i'|'r')))
Ldd: {Operation} opcode='ldd';
Lddr: {Operation} opcode='lddr';
Ldi: {Operation} opcode='ldi';
Ldir: {Operation} opcode='ldir';
Neg: {Operation} opcode='neg';
Nop: {Operation} opcode='nop';
Or: {Operation} opcode='or' operand=s;
Otdr: {Operation} opcode='otdr';
Otir: {Operation} opcode='otir';
Out: {Operation} opcode='out' ((CIndirect ',' rightOp=r)|(literal=NumericLiteralIndirect ',' REGISTER_A));
Outd: {Operation} opcode='outd';
Outi: {Operation} opcode='outi';
Pop: {Operation} opcode='pop' DoubleRegisters;
Push: {Operation} opcode='push' DoubleRegisters;
Res: {Operation} opcode='res' leftOp=B ',' rightOp=m;
Ret: {Operation} opcode='ret' CC?;
Reti: {Operation} opcode='reti';
Retn: {Operation} opcode='retn';
Rl: {Operation} opcode='rl' operand=m;
Rla: {Operation} opcode='rla';
Rlc: {Operation} opcode='rlc' operand=m;
Rlca: {Operation} opcode='rlca';
Rld: {Operation} opcode='rld';
RrCmd: {Operation} opcode='rr' operand=m;
Rra: {Operation} opcode='rra';
Rrca: {Operation} opcode='rrca';
Rrd: {Operation} opcode='rrd';
Rst: {Operation} opcode='rst' operand=p;
Sbc: {Operation} opcode='sbc' ((REGISTER_A ',' rightOp=s)|(REGISTER_HL ',' rightOp=ss));
Set: {Operation} opcode='set' leftOp=B ',' rightOp=m;
Sla: {Operation} opcode='sla' operand=m;
Sra: {Operation} opcode='sra' operand=m;
Srl: {Operation} opcode='srl' operand=m;
Sub: {Operation} opcode='sub' operand=s;
Xor: {Operation} opcode='xor' operand=s;

terminal SL_COMMENT     : ';' !('\n'|'\r')*;
terminal WS            : (' '|'\t')+;

B returns Operand: intType=INT; //'0'..'7'; //FIXME
NumericLiteral : BANKLABEL|referencedObj=[LabelType]|NUMBER; //ID
NUMBER: i=INT|str=(HEX|BINARY);
terminal HEX: ('$'('a'..'f'|'A'..'F'|'0'..'9')+)|('0'..'9'('a'..'f'|'A'..'F'|'0'..'9')*'h');
terminal BINARY: '%'('0'..'1')+;

AnonymousLabel: PLUS|PLUSPLUS|MINUS|MINUSMINUS;

CIndirect: ('[' REGISTER_C ']')|('(' REGISTER_C ')');
BCIndirect: ('[' REGISTER_BC ']')|('(' REGISTER_BC ')');
DEIndirect: ('[' REGISTER_DE ']')|('(' REGISTER_DE ')');
HLIndirect: ('[' REGISTER_HL ']')|('(' REGISTER_HL ')');
SPIndirect: ('[' REGISTER_SP ']')|('(' REGISTER_SP ')');
NumericLiteralIndirect: ('[' NumericLiteral ']')|('(' NumericLiteral ')');

IndexIndirectPlusOffset: ('[' IndexPlusOffset ']')|('(' IndexPlusOffset ')');
IndexIndirect: ('[' xx ']')|('(' xx ')');

IndexPlusOffset: xx PLUS INT;

CC: 'c'|'m'|'nc'|'nz'|'p'|'pe'|'po'|'z';
jrCc: 'c'|'nc'|'nz'|'z';

dst returns Operand: op=(s|ss)|reg=(BCIndirect|DEIndirect)|=>NumericLiteralIndirect;

e returns Operand: (lit=NumericLiteral|anonLabel=(PLUS|PLUSPLUS|MINUS|MINUSMINUS)); //fixme -126 to +129
m returns Operand: op=r|reg=(HLIndirect|IndexIndirectPlusOffset);
p returns Operand: intType=INT; //FIXME need to restrict to $0000, $0008, $0010, $0018, $0020, $0028, $0030 or $0038 
pp returns Operand: reg=(REGISTER_BC|REGISTER_DE|REGISTER_IX|REGISTER_SP);
r returns Operand: reg=(REGISTER_A|REGISTER_B|REGISTER_C|REGISTER_D|REGISTER_E|REGISTER_H|REGISTER_L);
rr returns Operand: reg=(REGISTER_BC|REGISTER_DE|REGISTER_IY|REGISTER_SP);
s returns Operand:  op=m|expr=Expression;
ss returns Operand: reg=(REGISTER_BC|REGISTER_DE|REGISTER_HL|REGISTER_SP);
src returns Operand: op=(s|ss)|reg=(BCIndirect|DEIndirect|REGISTER_I|REGISTER_R)|=>NumericLiteralIndirect;
xx: (REGISTER_IX|REGISTER_IY);
incOps returns Operand: op=(r|ss)|reg=(HLIndirect|xx|IndexIndirectPlusOffset);